buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'

        // Push related dependency
        classpath 'com.google.gms:google-services:3.0.0'
    }
}

repositories {
    jcenter()
    flatDir {
        dirs 'libs'
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    signingConfigs {
        debug {
            storeFile file("./keystore/debug.keystore")
        }

        release {
            storeFile file("./keystore/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    defaultConfig {
        applicationId "com.inrix.sample"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
            proguardFiles 'proguard-rules.pro',
                    'butterknife.pro',
                    'otto.pro',
                    'picasso.pro',
                    getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    // INRIX SDK.
    compile(name: 'com.inrix.sdk-9.0', ext: 'aar')
    compile(name: 'com.inrix.sdk.cache-1.6', ext: 'aar')

    // Google Play Services
    def playServicesVersion = "10.0.1"
    compile "com.google.android.gms:play-services-base:${playServicesVersion}"
    compile "com.google.android.gms:play-services-gcm:${playServicesVersion}"
    compile "com.google.android.gms:play-services-maps:${playServicesVersion}"
    compile "com.google.android.gms:play-services-location:${playServicesVersion}"

    // Support libraries.
    def androidSupportVersion = "25.3.1"
    compile "com.android.support:support-v4:${androidSupportVersion}"
    compile "com.android.support:recyclerview-v7:${androidSupportVersion}"
    compile "com.android.support:cardview-v7:${androidSupportVersion}"
    compile "com.android.support:appcompat-v7:${androidSupportVersion}"
    compile "com.android.support:design:${androidSupportVersion}"

    // Networking
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.android.volley:volley:1.0.0'

    // Push
    compile "com.google.firebase:firebase-messaging:${playServicesVersion}"

    // Logging.
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'com.github.tony19:logback-android-core:1.1.1-4'
    compile 'com.github.tony19:logback-android-classic:1.1.1-4'

    // UI.
    compile 'com.google.maps.android:android-maps-utils:0.5'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'
    compile 'com.github.castorflex.smoothprogressbar:library-circular:1.1.0'

    // OAuth login
    compile 'com.facebook.android:facebook-android-sdk:4.22.0'
}

// Push related plugin
apply plugin: 'com.google.gms.google-services'
